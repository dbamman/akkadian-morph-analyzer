! finish lowercasing accents on vowels, special consonants

read lexc /Users/david/Documents/git/akkadian-morph-analyzer/foma/noun.lex 
define NounLexicon ;

read lexc /Users/david/Documents/git/akkadian-morph-analyzer/foma/name.lex
define NameLexicon ;

read lexc /Users/david/Documents/git/akkadian-morph-analyzer/foma/adj.lex
define AdjLexicon ;

read lexc /Users/david/Documents/git/akkadian-morph-analyzer/foma/verbs.lex
define VerbLexicon ;

read lexc /Users/david/Documents/git/akkadian-morph-analyzer/foma/prep.lex
define PrepLexicon ;

read lexc /Users/david/Documents/git/akkadian-morph-analyzer/foma/default.lex
define UnknownLexicon ;

read lexc /Users/david/Documents/git/akkadian-morph-analyzer/foma/default_noun.lex
define UnknownNounLexicon ;

define Vowel [a|e|i|u|A|E|I|U|ā|ē|ī|ū] ;
define DINGIR D I N G I R ;
define GAL G A L ;
define DU10  D U 1 {0};
define Dental [d|t|ṭ|D|T];
define Sibilants [z|s|š|ṣ|ś|Z|S|Š|Ṣ|Ś|Z];

define Consonant [b|d|g|h|k|l|m|n|p|q|r|s|š|t|w|z|B|D|G|H|K|L|M|N|P|Q|R|S|Š|T|W|Z] ;

define GStemPreterite 	R1 -> i  || _ ?* [ %+GStem %+Pret %+3CS | %+GStem %+Pret %+3MP | %+GStem %+Pret %+3FP ] ,,
				R1 -> t a  || _ ?* [%+GStem %+Pret %+2MS |%+GStem %+Pret %+2FS |%+GStem %+Pret %+2CP] ,,
				R1 -> a  || _ ?* %+GStem %+Pret %+1CS ,,
				R1 -> n i  || _ ?* %+GStem %+Pret %+1CP ,,
				R4 -> ā  || _ ?* [%+GStem %+Pret %+3FP | %+GStem %+Pret %+2CP ] ,,
				R4 -> ū  || _ ?* %+GStem %+Pret %+3MP ,,
				R4 -> ī  || _ ?* %+GStem %+Pret %+2FS ;
				
define addPreteriteThemeVowel [..] -> a || _ R3 ?* "a" "^" PV1 ?* [ %+Pret | %+Imp ],,
								[..] -> e || _ R3 ?* "e" "^" PV1 ?* [ %+Pret | %+Imp ] ,,
								[..] -> o || _ R3 ?* "o" "^" PV1 ?* [ %+Pret | %+Imp ] ,,
								[..] -> i || _ R3 ?* "i" "^" PV1 ?* [ %+Pret | %+Imp ]  ;

define addDurativeThemeVowel [..] -> a || _ R3 ?* "a" "^" DV1 ?* [ %+Dur | %+Perf ] ,,
								[..] -> e || _ R3 ?* "e" "^" DV1 ?* [ %+Dur | %+Perf ] ,,
								[..] -> o || _ R3 ?* "o" "^" DV1 ?* [ %+Dur | %+Perf ] ,,
								[..] -> i || _ R3 ?* "i" "^" DV1 ?* [ %+Dur | %+Perf ] ;

define deleteEnd	Vowel PV1 -> 0 ,,
					Vowel DV1 -> 0 ,,
					"_" -> 0 ;

define GStemDurative 	R1 -> i  || _ ?* [ %+GStem %+Dur %+3CS | %+GStem %+Dur %+3MP | %+GStem %+Dur %+3FP ] ,,
				R1 -> t a  || _ ?* [%+GStem %+Dur %+2MS |%+GStem %+Dur %+2FS |%+GStem %+Dur %+2CP] ,,
				R1 -> a  || _ ?* %+GStem %+Dur %+1CS ,,
				R1 -> n i  || _ ?* %+GStem %+Dur %+1CP ,,
				R4 -> ā  || _ ?* [%+GStem %+Dur %+3FP | %+GStem %+Dur %+2CP ] ,,
				R4 -> ū  || _ ?* %+GStem %+Dur %+3MP ,,
				R4 -> ī  || _ ?* %+GStem %+Dur %+2FS ,,
				R2 -> a  || _ ?* %+GStem %+Dur ?* ,,
				b -> b b , d -> d d , g -> g g , h -> h h , k -> k k , l -> l l , m -> m m , n -> n n , p -> p p , q -> q q , r -> r r , s -> s s , š -> š š , t -> t t , w -> w w , z -> z z  || _ "^" R3  ?* %+GStem %+Dur ;

define GStemImperative	R4 -> ā  || _ ?* %+GStem %+Imp %+2CP ,,
				R4 -> ī  || _ ?* %+GStem %+Imp %+2FS ,,
				R2 -> a || _ ?* "a" "^" PV1 ?* %+GStem %+Imp ,,
				R2 -> e || _ ?* "e" "^" PV1 ?* %+GStem %+Imp ,,
				R2 -> i || _ ?* "i" "^" PV1 ?* %+GStem %+Imp ,,
				R2 -> u || _ ?* "u" "^" PV1 ?* %+GStem %+Imp ,,
				Vowel (->) 0 || _ R3 ?* %+GStem %+Imp %+2CP , _ R3 ?* %+GStem %+Imp %+2FS ;				

define GStemPerfect 	R1 -> i  || _ ?* [ %+GStem %+Perf %+3CS | %+GStem %+Perf %+3MP | %+GStem %+Perf %+3FP ] ,,
				R1 -> t a  || _ ?* [%+GStem %+Perf %+2MS |%+GStem %+Perf %+2FS |%+GStem %+Perf %+2CP] ,,
				R1 -> a  || _ ?* %+GStem %+Perf %+1CS ,,
				R1 -> n i  || _ ?* %+GStem %+Perf %+1CP ,,
				R4 -> ā  || _ ?* [%+GStem %+Perf %+3FP | %+GStem %+Perf %+2CP ] ,,
				R4 -> ū  || _ ?* %+GStem %+Perf %+3MP ,,
				R4 -> ī  || _ ?* %+GStem %+Perf %+2FS ,,
				R2 -> t a || _ ?* %+GStem %+Perf ,,
				Vowel -> 0 || _ R3 ?* %+GStem %+Perf [%+2CP| %+2FS | %+3FP | %+3MP ] ;	

define DStemPreterite 	R1 -> u  || _ ?* %+DStem %+Pret [ %+3CS | %+3MP | %+3FP | %+1CS ] ,,
				R1 -> t u  || _ ?* %+DStem %+Pret [ %+2MS | %+2FS | %+2CP ] ,,
				R1 -> n u  || _ ?* %+DStem %+Pret %+1CP ,,
				R4 -> ā  || _ ?* [%+DStem %+Pret %+3FP | %+DStem %+Pret %+2CP ] ,,
				R4 -> ū  || _ ?* %+DStem %+Pret %+3MP ,,
				R4 -> ī  || _ ?* %+DStem %+Pret %+2FS ,,
				Vowel -> i || _ R3 ?* %+DStem %+Pret ,, 
				R2 -> a  || _ ?* %+DStem %+Pret ?* ;

define DStemDurative 	R1 -> u  || _ ?* %+DStem %+Dur [ %+3CS | %+3MP | %+3FP | %+1CS ] ,,
				R1 -> t u  || _ ?* %+DStem %+Dur [ %+2MS | %+2FS | %+2CP ] ,,
				R1 -> n u  || _ ?* %+DStem %+Dur %+1CP ,,
				R4 -> ā  || _ ?* [%+DStem %+Dur %+3FP | %+DStem %+Dur %+2CP ] ,,
				R4 -> ū  || _ ?* %+DStem %+Dur %+3MP ,,
				R4 -> ī  || _ ?* %+DStem %+Dur %+2FS ,,
				Vowel -> a || _ R3 ?* %+DStem %+Dur ,, 
				R2 -> a  || _ ?* %+DStem %+Dur ?* ;

define DStemPerfect	R1 -> u  || _ ?* %+DStem %+Perf [ %+3CS | %+3MP | %+3FP | %+1CS ] ,,
				R1 -> t u  || _ ?* %+DStem %+Perf [ %+2MS | %+2FS | %+2CP ] ,,
				R1 -> n u  || _ ?* %+DStem %+Perf %+1CP ,,
				R4 -> ā  || _ ?* [%+DStem %+Perf %+3FP | %+DStem %+Perf %+2CP ] ,,
				R4 -> ū  || _ ?* %+DStem %+Perf %+3MP ,,
				R4 -> ī  || _ ?* %+DStem %+Perf %+2FS ,,
				Vowel -> i || _ R3 ?* %+DStem %+Perf ,, 
				R2 -> t a || _ ?* %+DStem %+Perf ;			

define DStemImperative	R4 -> ā  || _ ?* %+DStem %+Imp %+2CP ,,
				R4 -> ī  || _ ?* %+DStem %+Imp %+2FS ,,
				R2 -> u || _ ?* %+DStem %+Imp ,,
				Vowel -> i || _ R3 ?* %+DStem %+Imp ;				

define DStemDoubling b -> b b , d -> d d , g -> g g , h -> h h , k -> k k , l -> l l , m -> m m , n -> n n , p -> p p , q -> q q , r -> r r , s -> s s , š -> š š , t -> t t , w -> w w , z -> z z  || _ "^" R3  ?* %+DStem ;

define deleteWeakRadicals ALEPH -> 0 ;

define SStemPreterite 	R1 -> u š a  || _ ?* %+SStem %+Pret [ %+3CS | %+3MP | %+3FP | %+1CS ] ,,
				R1 -> t u š a  || _ ?* %+SStem %+Pret [ %+2MS | %+2FS | %+2CP ] ,,
				R1 -> n u š a  || _ ?* %+SStem %+Pret %+1CP ,,
				R4 -> ā  || _ ?* [%+SStem %+Pret %+3FP | %+SStem %+Pret %+2CP ] ,,
				R4 -> ū  || _ ?* %+SStem %+Pret %+3MP ,,
				R4 -> ī  || _ ?* %+SStem %+Pret %+2FS ,,
				Vowel -> i || _ R3 ?* %+SStem %+Pret ;

define SStemDurative 	R1 -> u š a  || _ ?* %+SStem %+Dur [ %+3CS | %+3MP | %+3FP | %+1CS ] ,,
				R1 -> t u š a  || _ ?* %+SStem %+Dur [ %+2MS | %+2FS | %+2CP ] ,,
				R1 -> n u š a  || _ ?* %+SStem %+Dur %+1CP ,,
				R4 -> ā  || _ ?* [%+SStem %+Dur %+3FP | %+SStem %+Dur %+2CP ] ,,
				R4 -> ū  || _ ?* %+SStem %+Dur %+3MP ,,
				R4 -> ī  || _ ?* %+SStem %+Dur %+2FS ,,
				Vowel -> a || _ R3 ?* %+SStem %+Dur ;

define SStemPerfect	R1 -> u š t a  || _ ?* %+SStem %+Perf [ %+3CS | %+3MP | %+3FP | %+1CS ] ,,
				R1 -> t u š t a  || _ ?* %+SStem %+Perf [ %+2MS | %+2FS | %+2CP ] ,,
				R1 -> n u š t a  || _ ?* %+SStem %+Perf %+1CP ,,
				R4 -> ā  || _ ?* [%+SStem %+Perf %+3FP | %+SStem %+Perf %+2CP ] ,,
				R4 -> ū  || _ ?* %+SStem %+Perf %+3MP ,,
				R4 -> ī  || _ ?* %+SStem %+Perf %+2FS ,,
				Vowel -> i || _ R3 ?* %+SStem %+Perf ;

define SStemImperative	R1 -> š u  || _ ?* %+SStem %+Imp ,,
				R4 -> ā  || _ ?* %+SStem %+Imp %+2CP ,,
				R4 -> ī  || _ ?* %+SStem %+Imp %+2FS ,,
				Vowel -> i || _ R3 ?* %+SStem %+Imp ;

define NStemPreterite 	R1 -> i n  || _ ?* [ %+NStem %+Pret %+3CS | %+NStem %+Pret %+3MP | %+NStem %+Pret %+3FP ] ,,
				R1 -> t a n  || _ ?* [%+NStem %+Pret %+2MS |%+NStem %+Pret %+2FS |%+NStem %+Pret %+2CP] ,,
				R1 -> a n  || _ ?* %+NStem %+Pret %+1CS ,,
				R1 -> n i n  || _ ?* %+NStem %+Pret %+1CP ,,
				R4 -> ā  || _ ?* [%+NStem %+Pret %+3FP | %+NStem %+Pret %+2CP ] ,,
				R4 -> ū  || _ ?* %+NStem %+Pret %+3MP ,,
				R4 -> ī  || _ ?* %+NStem %+Pret %+2FS ,,
				Vowel -> i || _ R3 ?* %+NStem %+Pret ;

define NStemDurative 	R1 -> i n  || _ ?* [ %+NStem %+Dur %+3CS | %+NStem %+Dur %+3MP | %+NStem %+Dur %+3FP ] ,,
				R1 -> t a n  || _ ?* [%+NStem %+Dur %+2MS |%+NStem %+Dur %+2FS |%+NStem %+Dur %+2CP] ,,
				R1 -> a n  || _ ?* %+NStem %+Dur %+1CS ,,
				R1 -> n i n  || _ ?* %+NStem %+Dur %+1CP ,,
				R4 -> ā  || _ ?* [%+NStem %+Dur %+3FP | %+NStem %+Dur %+2CP ] ,,
				R4 -> ū  || _ ?* %+NStem %+Dur %+3MP ,,
				R4 -> ī  || _ ?* %+NStem %+Dur %+2FS ,,
				R2 -> a  || _ ?* %+NStem %+Dur ?* ,,
				b -> b b , d -> d d , g -> g g , h -> h h , k -> k k , l -> l l , m -> m m , n -> n n , p -> p p , q -> q q , r -> r r , s -> s s , š -> š š , t -> t t , w -> w w , z -> z z  || _ "^" R3  ?* %+NStem %+Dur ;

define NStemPerfect 	R1 -> i n t a  || _ ?* [ %+NStem %+Perf %+3CS | %+NStem %+Perf %+3MP | %+NStem %+Perf %+3FP ] ,,
				R1 -> t a n t a  || _ ?* [%+NStem %+Perf %+2MS |%+NStem %+Perf %+2FS |%+NStem %+Perf %+2CP] ,,
				R1 -> a n t a  || _ ?* %+NStem %+Perf %+1CS ,,
				R1 -> n i n t a  || _ ?* %+NStem %+Perf %+1CP ,,
				R4 -> ā  || _ ?* [%+NStem %+Perf %+3FP | %+NStem %+Perf %+2CP ] ,,
				R4 -> ū  || _ ?* %+NStem %+Perf %+3MP ,,
				R4 -> ī  || _ ?* %+NStem %+Perf %+2FS ;
				
define NStemImperative	R1 -> n a  || _ ?* %+NStem %+Imp ,,
				R4 -> ā  || _ ?* %+NStem %+Imp %+2CP ,,
				R4 -> ī  || _ ?* %+NStem %+Imp %+2FS ,,
				Vowel -> i || _ R3 ?* %+NStem %+Imp ;


define VerbCleanup [R1|R2|R3|R4|V1|"^"] -> 0 ;
define SymbolCleanup [R1|%+GStem|%+SStem|%+NStem|%+DStem|%+Perf|%+2FS|%+Pret|%+Dur|%+3CS|%+2CP|%+3FP|%+3MP|%+1CS|%+1CP|%+2MS|%+2FS|%+Imp] -> 0 ;

define VerbGrammar VerbLexicon .o. addPreteriteThemeVowel .o. addDurativeThemeVowel .o. GStemPreterite .o. DStemPreterite .o. GStemDurative .o. GStemImperative .o. GStemPerfect  .o. DStemDurative .o. DStemPerfect .o. DStemImperative .o. DStemDoubling  .o. SStemPreterite .o. SStemDurative .o. SStemPerfect .o. SStemImperative  .o. NStemPreterite .o. NStemDurative .o. NStemPerfect .o. NStemImperative .o. VerbCleanup .o. SymbolCleanup .o. deleteWeakRadicals .o. deleteEnd ; ! ;.o. 



define Cleanup "^" -> 0 ,,
				"y" -> 0 ,,
				"/" -> 0 ,,
				"*" -> 0 ,,
				"." -> 0 ,,
				"[" -> 0 ,,
				"]" -> 0 ,,
				"--" -> "-" ,,
				"-" -> 0;

define StripAlephs [ˀ|'] -> 0;

define Sign [A|A N {.} N A|A N {.} N A {.} a|A N {.} N A {.} a k|A N {.} N A {.} k i|A N D U L|A N Š E {.} H I|A N Š E {.} h i|A h|A l|A m|A r|A š u r|A š š u r|B A|B a|B e|B u|B u r|B é|D A M {.} G À R|D A M {.} Q A R|D A M {.} Q A R {.} r i|D I N G I R|D U 1 {0}|D U M U|D a|D a m|D a n|D u|D Ù G|E|E n|E r|G A|G A L|G I 6|G a|G Í N|G Í N {.} T A|G Ú|H a|H i|H u|I|I R|I T U {.} 1 {.} K A M|I b|I d|I d d i n|I l 5|I m|I n|I r|I s|I š|I š t a r|K B|K i|K u|K u r|K Ù {.} B|K Ù {.} B {.} a p|K Ù {.} B {.} p í|K Ù {.} B A B B A R|K Ù {.} B A B B A R {.} a p|K Ù {.} B A B B A R {.} p ì|K Ù {.} B A B B A R {.} p í|K Ù {.} B A B B A R {.} á p|K Ù {.} G I|K Ù {.} p ì|K à|K à r|L U G A L|L a|L i|L o E a|L u|L á|M A R {.} T U|M a|M e|M u|N I|N a|N i|N u|P u|P u z u r|P u z u r 4|P u z ú r|P á|P è|P ì|P í|Q a r|Q á|R a s {.}|S A {.} T U|S I G 5|S I P A|S u|S à|S á|S ú|T U G k u|T a|T i|T u r 4|T Ú G|T Ú G {.} H I|T Ú G {.} H I {.} A {.} t í|T Ú G {.} H I {.} t í|T Ú G {.} H I {.} t ù|T Ú G {.} t i|T Ú G {.} t í|T Ú G k u|T á r|T é|T í|T ù|U|U R U D U|U R U D U {.} a|U R U D U {.} i|U T U|U T U {.} š i|U m|U r|U š|W a|W e|Z I|Z U|Z a|Z i|Z u|Z z {.}|a|a b|a d|a g|a h|a k|a l|a m|a n|a p|a q|a r|a s|a t|a z|a š|a ṣ|a ṭ|b a|b a r|b e|b i|b i 4|b i l|b u|b u r|b á|b é|b ì|b í|c a {.}|d|d {.} A|d {.} E N {.} L I L 2|d {.} E N {.} Z U|d {.} E n|d {.} I M|d {.} M A R {.} T U|d {.} M U Š|d {.} N I N {.} Š U B U R|d {.} U T U|d {.} U T U {.} t a p|d {.} U T U {.} š i|d I M|d M A R {.} T U|d U T U|d a|d a m|d a n|d a r|d e 8|d e 9|d i|d i m|d u|d u m|d á|d ì|d í|e|e b|e h|e l|e m|e n|e n 6|e p|e q|e r|e t|e z|e š|e ṣ|e ṭ|e š 1 5|g a|g a l|g a m|g e 5|g i|g i 5|g u|g u 5|g à r|g á m|g á n|h a|h e|h i|h u|i|i b|i d|i g|i h|i k|i l|i l 5|i m|i n|i n 4|i p|i q|i r|i s|i t|i z|i š|i ṣ|i ṭ|i š {.} k i|k a|k a m|k e|k i|k u|k u l|k u m|k u r|k à|k á|k á l|k á n|k ú l|l a|l a 1|l a m|l a m 5|l e|l i|l i k|l i m|l i m {.} K I|l i m {.} k i|l i m k i|l i q|l u|l á|l í|l í l|l ú m|m {.} I|m a|m a r|m a »|m a š|m e|m i|m u|m u r|m ì|n a|n a {.} T A|n a m|n i|n i m|n u|n u m|n à m|n é|p a|p e|p i|p u|p u r|p à|p á|p á r|p è|p é|p ì|p í|q a|q a l|q a m|q a r|q e|q i|q u|q u l|q á|q á m|q é|q í|q ú|q ú l|q ú r|r a|r e|r i|r i k|r u|s a|s i|s u|s à|s á|s é|s í|s ú|t a|t a b|t a l|t a m|t a m 4|t a p|t a r|t a š|t e 9|t e m|t i|t i m|t u|t u m|t u m 8|t u p|t u r 4|t á|t á k|t á m|t á r|t è|t é|t ì|t í|t í m|t ù|t ú|u|u 4|u b|u d|u g|u h|u k|u l|u m|u n|u p|u q|u r|u s|u t|u z|u š|u ṣ|u ṭ|u š 1 0|w a|w e|w i|w u|x|z a|z a l|z e|z i|z u|Á b|Á s|Ú|á b|á p|á š|ì|ì š|í|ù|ù h|ú|ú h|ú s|ú z|ú ṣ|Š E|Š E Š|Š a|š a|ṣ a|ṭ a|š a l|Š a r|š a r|š e|ṣ e|š i|ṣ i|ṭ i m|Š u|š u|ṣ u|š u m|ṭ u p|š u r|ṣ u r|ṭ u r 4|Ṭ á|ṭ á|Š á l|š á l|ṭ á m|Š é|š é|ṣ é|ṭ é|ṭ ì|Š í|š í|Ṣ í|ṣ í|ṭ í|ṭ ù|š ù r|š ú|ṣ ú|š ú r|T A];

!define Sign [a|a b|a d|a g|a l|a m|a n|a r|a š|a z|b a|b a d|b a g|b e|b é|b i|b í|b i d|b í d|b i l|b u|d a|d a b|d a l|d a m|d a n|d a r|d e|d i|d i b|d i l|d i m|d u|d ú r|e|e b|e d|e g|e h h|e l|e m|e n|è r|e r i|e š|e z|g a|g a l|g á m|g a š|g e|g i|g i d|g í d|g i n|g i š|g i z|g u b|g ú l|h a|h a d|h a l|h e|h i|h i r|h i š|h u|i|ì|i 1 4|i a|i a 5|i b|i d|i e|i g|i h|i i|i l|i l 5|i m|i n|i n a|i r|ì r|i r i|i š|i š 8|i u|i z|k a|k a l|k a m|k à r|k á s|k a š|k e|k i|k i d|k i n|k u|k u l|k u r|l a|l a b|l a d|l a h|l a m|l e|l i|l í|l í b|l ì b|l i g|l i h|l í l|l i m|l u|l u 4|l u m|m a|m a d|m a r|m e|m é|m e š|m i|m i d|m ì l|m i m|m u|n a|n a d|n e|n è|n i|n i d|n i m|n i n|n u|n ù|n u m|p a|p a g|p a r|p e|p é|p i|p í|p í d|p i l|p i r|p u|q á|q a d|q a l|q á m|q a r|q é|q í|q i d|q í d|q ú|q u b|q u l|q ú r|r a|r a g|r e|r é|r i|r í|r i b|r u|r ù|r u m|s à|s a h|s a l|s a r|s e|s é|s i|s í|s i b|s í h|s i n|s i r|s ú|s u h 4|s ú|s u n|ṣ a|ṣ a l|ṣ e|ṣ é|ṣ i|ṣ í|ṣ í l|ṣ u|ṣ ú|ṣ u m|ṣ u r|š a|š à|š a d|š a l|š a m|š a r|š à r|š e|š i|š i b|š i r|š ì r|š u|š u b|š u m|t a|t a b|t a g|t a l|t a m|t á r|t a š|t e|t i|t i l|t i m|t í š|t u|t u m|t ú|t u r|t u š|ṭ a|ṭ á|ṭ a l|ṭ a m|ṭ e|ṭ è|ṭ e 4|ṭ i|ṭ ì|ṭ i l|ṭ u|ṭ ù|ú|ù|u 4|u d|u g|u h|u l|u m|u r|ú r|ú s|u š|ú š|u z|w a|w e|w i|w u|z a|z a l|z e|z è|z i|z í|z i r|z i z|z u|z u m|z u r];

define LowerCase	A (->) a ,, 
					B (->) b ,, 
					D (->) d ,, 
					E (->) e ,, 
					G (->) g ,, 
					H (->) h ,, 
					I (->) i ,, 
					K (->) k ,, 
					L (->) l ,, 
					M (->) m ,, 
					N (->) n ,, 
					P (->) p ,,
					Q (->) q ,, 
					R (->) r ,, 
					S (->) s ,, 
					T (->) t ,, 
					U (->) u ,, 
					W (->) w ,, 
					Z (->) z;

define StripLong		ā -> a ,,
						ē -> e ,,
						ī -> i ,,
						û -> u ,,
						ū -> u ;

define NasalElision 	[m|n|t] (->) 0 ||  Vowel _ Consonant ;

define VowelDoubling 	a (->) a a ,,
						e (->) e e ,,
						i (->) i i ,,
						u (->) u u ;

define ConsonantSingling	b b (->) b ,,
							d d (->) d ,,
							g g (->) g ,,
							h h (->) h ,,
							ḫ ḫ (->) ḫ ,,
							k k (->) k ,,
							l l (->) l ,,
							m m (->) m ,,
							n n (->) n ,,
							p p (->) p ,,
							q q (->) q ,,
							r r (->) r ,,
							s s (->) s ,,
							š š (->) š ,,
							ṣ ṣ (->) ṣ ,,
							q q (->) q ,,
							t t  (->) t ,,
							ṭ ṭ (->) q ,,
							w w (->) w ,,
							z z (->) z ;

define Contraction	a a (->) a ,,
					a i (->) e ,,
					a u (->) u ,,
					i a (->) a ,,
					i i (->) i ,,
					i u (->) u ,,
					u a (->) a ,,
					!u i (->) i ,,
					u u (->) u ,,
					i l u m (->) DINGIR ,,
					a d a d (->) d {.} I M ,,
					a d a d (->) DINGIR {.} I M ,,
					i l i (->) DINGIR ,,
					i l i m (->) DINGIR ,,
					r a b i (->) GAL ,,
					t a m k a r (->) {DAM.QAR} ,,
					ṭ a b (->) DU10 ,,
					k a s (->) K Ù {.} B A B B A R ,,
					k a s (->) K U G {.} B A B B A R ,,
					a n n a (->) A N {.} N A ,,
					Dental Sibilant (->) s s ;

define Het2H	ḫ (->) h ;


define AcuteAccents		a (->) á ,,
						e (->) é ,,
						i (->) í ,,
						i (->) e ,,
						u (->) ú ;

define NoMimation	m (->) 0 || _ .#. ;

define GraveAccents		a (->) à ,,
						e (->) è ,,
						i (->) ì ,,
						u (->) ù ;

define HigherSigns	Sign (->) ... "2" ,,
					Sign (->) ... "3" ,,
					Sign (->) ... "4" ,,
					Sign (->) ... "5" ,,
					Sign (->) ... "6" ,,
					Sign (->) ... "7" ,,
					Sign (->) ... "8" ,,
					Sign (->) ... "9" ;
					!Sign (->) ... "x" ,
					!Sign (->) ... "X" ;

define MarkSigns Sign (->) ... "-" || _ Sign ;
define SyllabizedWord [ Sign "-"]* Sign ;

define DotsOrHyphens "-" (->) {.} ;

define Anything	[..] -> ?+ ;
define BeforeAnything	[..] -> ?+ || _ [ u m |a m|i m];

define Unknown UnknownLexicon .o. Anything ;
define PrefixUnknown UnknownNounLexicon .o. BeforeAnything ;
define Unk PrefixUnknown .p. Unknown ;

define Normalize LowerCase .o. StripAlephs .o. Cleanup .o. StripLong .o. NasalElision .o. VowelDoubling .o. ConsonantSingling .o. Contraction .o. AcuteAccents .o. GraveAccents .o. NoMimation .o. HigherSigns .o. MarkSigns .o. SyllabizedWord .o. DotsOrHyphens ;

define FinalVerbGrammar ( VerbGrammar .o. Cleanup  .o. Normalize );
define RestGrammar ( (NounLexicon | NameLexicon | AdjLexicon | PrepLexicon) .o. Cleanup .o. Normalize );

regex  ( FinalVerbGrammar | RestGrammar ) .p. Unk ;
